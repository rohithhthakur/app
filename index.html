// Stage to build or deploy models in parallel
stage("Build or Deploy Models") {
    def modelJobs = [:]
    models.each { model ->
        modelJobs[model] = {
            stage("Deploy Model: ${model}") {
                def model_config = readJSON(file: "models/${model}/configuration.json")
                def versionsToDeploy = model_config.versions
                def versionJobs = [:]  // Define a map to hold version jobs for parallel execution
                if (params.Version != 'All' && params.Version.trim()) {
                    versionsToDeploy = versionsToDeploy.findAll { it.version == params.Version }
                    if (versionsToDeploy.isEmpty()) {
                        error "Specified version '${params.Version}' not found in model '${model}'. Please check the version."
                    }
                }
                versionsToDeploy.each { version ->
                    versionJobs["${model}_${version.version}"] = {
                        def branch = "releases%2Frelease-${version.version.replace('/', '%2F')}"
                        if (model == "allm3_challenger") {
                            branch = "releases%2Fchallenger%2Frelease-${version.version}"
                        } else if (version.version.contains("allm4") && model == "allm3_spark") {
                            branch = "releases%2F" + version.version.replace('/', '%2F')
                        } else if (model == "ccrm-dagger-utils") {
                            branch = "master"
                        }
                        def job = "${DEPLOY_FOLDER_PATH}${model_config.deploy_pipeline}/${branch}"
                        try {
                            build job: job
                            println("Successfully deployed ${model} version ${version.version}")
                        } catch(Exception e) {
                            error("Failed to deploy ${model} version ${version.version}")
                        }
                    }
                }
                parallel versionJobs  // Execute all version jobs in parallel
            }
        }
    }
    parallel modelJobs
}